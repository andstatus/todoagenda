apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    useLibrary 'android.test.mock'

    androidResources {
        generateLocaleConfig true
    }

    defaultConfig {
        versionCode 717
        versionName '4.13.3'
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "TodoAgenda-$versionName".toString()

        buildConfigField "String", "ORG_TASKS_AUTHORITY", "\"org.tasks\""
    }

    buildTypes {
        release {
            minifyEnabled false
            lintOptions {
                warning 'MissingTranslation','InvalidPackage'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lint {
        abortOnError false
    }
    namespace 'org.andstatus.todoagenda'
    buildFeatures {
        buildConfig true
    }

    if (project.hasProperty('todoagendaStoreFile')) {
        signingConfigs {
            releaseConfig {
                storeFile file(todoagendaStoreFile)
                storePassword todoagendaStorePassword
                keyAlias todoagendaKeyAlias
                keyPassword todoagendaKeyPassword
            }
        }
        buildTypes.release.signingConfig signingConfigs.releaseConfig
    }
    // See https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
    if (project.hasProperty("todoagenda.signing")
            && new File(project.property("todoagenda.signing").toString() + ".gradle").exists()) {
        apply from: project.property("todoagenda.signing") + ".gradle";
    }
}

dependencies {
    implementation "joda-time:joda-time:$jodaTimeVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.preference:preference:$preferenceVersion"
    implementation project(':colorpicker')
    implementation "androidx.core:core-ktx:$ktxVersion"

    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test:core:$testCoreVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
}
